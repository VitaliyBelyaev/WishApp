plugins {
    id "com.android.application"
    id "org.jetbrains.kotlin.android"
    id "kotlin-parcelize"
    id "kotlin-kapt"
    id "dagger.hilt.android.plugin"
    id "com.squareup.sqldelight"
    id "com.google.gms.google-services"
    id "com.google.firebase.crashlytics"
    id "com.google.firebase.firebase-perf"
    id "com.google.android.gms.oss-licenses-plugin"
}

android {
    compileSdk 33

    namespace 'ru.vitaliy.belyaev.wishapp'
    defaultConfig {
        applicationId "ru.vitaliy.belyaev.wishapp"
        minSdk 23
        targetSdk 33
        versionCode 16
        versionName "1.4.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion "1.4.0-alpha02"
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }

    signingConfigs {
        signingTest {
            storeFile file('test_keystore')
            storePassword "notasecret"
            keyAlias "test"
            keyPassword "notasecret"
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            resValue "string", "release_app_name", "@string/debug_app_name"
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            resValue "string", "release_app_name", "@string/app_name"
        }
    }
}

dependencies {
    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:1.2.0"

    // Android X common
    implementation "androidx.core:core-ktx:1.9.0"
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.5.1"
    implementation "androidx.datastore:datastore-preferences:1.0.0"
    implementation "androidx.appcompat:appcompat:1.6.0"
    implementation "androidx.webkit:webkit:1.5.0"
    implementation "androidx.core:core-splashscreen:1.0.0"
    implementation "androidx.activity:activity-compose:1.6.1"
    implementation "androidx.constraintlayout:constraintlayout-compose:1.0.1"

    // Compose
    implementation platform("androidx.compose:compose-bom:2023.01.00")
    implementation "androidx.compose.ui:ui:1.4.0-alpha04"
    implementation "androidx.compose.foundation:foundation:1.4.0-alpha04"
    implementation "androidx.compose.material3:material3:1.1.0-alpha04"
    implementation "androidx.compose.material:material"
    implementation "androidx.compose.ui:ui-tooling-preview"

    // Compose Navigation
    implementation "androidx.navigation:navigation-compose:2.5.3"
    implementation "androidx.hilt:hilt-navigation-compose:1.0.0"

    // Google Accompanist
    implementation "com.google.accompanist:accompanist-flowlayout:$accompanistVesrion"
    implementation "com.google.accompanist:accompanist-navigation-animation:$accompanistVesrion"
    implementation "com.google.accompanist:accompanist-insets:$accompanistVesrion"
    implementation "com.google.accompanist:accompanist-systemuicontroller:$accompanistVesrion"

    // Google Android
    implementation "com.google.android.material:material:1.7.0"
    implementation "com.google.android.gms:play-services-oss-licenses:17.0.0"
    implementation "com.google.android.play:core:1.10.3"

    // Google Firebase
    implementation platform("com.google.firebase:firebase-bom:31.1.1")
    implementation "com.google.firebase:firebase-analytics-ktx"
    implementation "com.google.firebase:firebase-crashlytics-ktx"
    implementation "com.google.firebase:firebase-perf-ktx"

    // Hilt
    implementation "com.google.dagger:hilt-android:$hiltVersion"
    kapt "com.google.dagger:hilt-android-compiler:$hiltVersion"

    // Database
    implementation "com.squareup.sqldelight:android-driver:$sqlDelightVersion"
    implementation "com.squareup.sqldelight:coroutines-extensions-jvm:$sqlDelightVersion"

    // Okhttp
    implementation(platform("com.squareup.okhttp3:okhttp-bom:4.9.1"))
    implementation("com.squareup.okhttp3:okhttp")
    implementation("com.squareup.okhttp3:logging-interceptor")

    // Jsoup
    implementation "org.jsoup:jsoup:1.15.2"

    // Coil
    implementation "io.coil-kt:coil-compose:2.2.2"

    // Timber
    implementation "com.jakewharton.timber:timber:5.0.1"

    // LeakCanary
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.9.1'

    // Shimmer animation for loading
    implementation "com.valentinilk.shimmer:compose-shimmer:1.0.1"

    implementation "org.burnoutcrew.composereorderable:reorderable:0.9.6"

    // JUnit
    testImplementation "junit:junit:4.13.2"
    testImplementation "org.hamcrest:hamcrest-junit:2.0.0.0"

    // Mockito
    testImplementation "org.mockito:mockito-core:3.11.2"
    testImplementation "org.mockito:mockito-inline:3.11.2"
    testImplementation "com.squareup.sqldelight:sqlite-driver:$sqlDelightVersion"
    testImplementation('org.xerial:sqlite-jdbc:3.8.10.2') {
        // Override the version of sqlite used by sqlite-driver to match Android API 23
        force = true
    }

    // Instrumentation
    androidTestImplementation "androidx.test:runner:1.5.2"
    androidTestImplementation "androidx.test.ext:junit:1.1.5"
    androidTestImplementation "androidx.compose.ui:ui-test-junit4"
    debugImplementation "androidx.compose.ui:ui-tooling"
}

sqldelight {
    WishAppDb {
        packageName = "ru.vitaliy.belyaev.wishapp.data.database"
        dialect = "sqlite:3.24"
        schemaOutputDirectory = file("src/main/sqldelight/databases")
        verifyMigrations = true
    }
}